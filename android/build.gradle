
apply plugin: 'maven-publish'
apply plugin: 'com.android.library'
apply plugin: 'com.kezong.fat-aar'
apply plugin: 'com.jfrog.bintray'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

group properties.getProperty("bintray.groupId")
version properties.getProperty("bintray.version")

String grpcVersion = "1.31.0"
String protobufVersion = "3.12.4"

android {
    compileSdkVersion 30
    defaultConfig {
        consumerProguardFiles "proguard-rules.txt"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    lintOptions { abortOnError false }
    sourceSets {
        main.java.srcDirs += 'src/main/grpc'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations.embed.transitive = true

dependencies {
    api "io.grpc:grpc-stub:$grpcVersion"
    api "io.grpc:grpc-okhttp:$grpcVersion"

    api "io.grpc:grpc-protobuf-lite:$grpcVersion"
    api "com.google.protobuf:protobuf-javalite:$protobufVersion"

    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions {
    doLast {
        println project.getConvention()
    }
}

bintray {
    dryRun = false
    publish = true
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.key")

    publications = ['release']
    pkg {
        repo = properties.getProperty("bintray.repo")
        name = properties.getProperty("bintray.package")
        userOrg = properties.getProperty("bintray.organization")
        vcsUrl = properties.getProperty("bintray.vcsUrl")
        licenses = ['MIT']
        labels = ['android', 'elarian', 'sms', 'telegram', 'whatsapp', 'voice', 'ussd', 'airtime', 'payment']
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = properties.getProperty("bintray.groupId")
                artifactId = project.name
                version = project.version
            }
        }
    }
}