Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

group properties.getProperty("bintray.groupId")
version properties.getProperty("bintray.version")

apply plugin: 'java'
apply plugin: "maven-publish"
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.johnrengelman.shadow'

String protobufVersion = "3.14.0"

dependencies {
    compile "com.google.protobuf:protobuf-java:$protobufVersion"
    compile 'io.rsocket:rsocket-core:1.1.0'
    compile 'io.rsocket:rsocket-transport-netty:1.1.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}


jar {
    manifest {
        attributes 'Implementation-Title': 'Elarian SDK',
                'Implementation-Version': version
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compile
    options {
        links "http://docs.oracle.com/javase/7/docs/api/"
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

shadowJar {
    classifier = null // Avoid classifiers for now...
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        SDK(MavenPublication) {
            artifact shadowJar
            groupId properties.getProperty("bintray.groupId")
            artifactId project.name
            version project.version
        }
    }
}

bintray {
    dryRun = false
    publish = true
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.key")
    publications = ['SDK']
    pkg {
        repo = properties.getProperty("bintray.repo")
        name = properties.getProperty("bintray.package")
        userOrg = properties.getProperty("bintray.organization")
        vcsUrl = properties.getProperty("bintray.vcsUrl")
        licenses = ['MIT']
        version {
            name = project.version
            desc = 'Official Elarian SDK'
            released  = new Date()
            attributes = ['maturity': 'Official']
        }
    }
}